import com.sun.javafx.PlatformUtil

buildscript {

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
        classpath 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.11'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
    }
}

ext {
    applicationName = 'SlideshowFX'
    applicationVersion = '1.0.0'

    allLibs = new File(rootDir, '/lib')
    jdk = System.env.'JAVA_HOME'

    pluginsFolder = new File(System.getProperty("user.home"), '.SlideshowFX/plugins')
    felixCacheFolder = new File(System.getProperty("user.home"), '.SlideshowFX/felix-cache')
}

allprojects {

    repositories {
        jcenter()
        mavenCentral()
    }

    configurations {
        asciidoctorj
        drive
        dropbox
        felix
        fontawesomefx
        freemarker
        jsoup
        leapmotion
        markdown
        twitter4j
        vertx
        wikitext
        zxing

        junit
        mockito
    }

    dependencies {
        asciidoctorj 'org.asciidoctor:asciidoctorj:1.5.4'
        drive 'com.google.apis:google-api-services-drive:v2-rev213-1.21.0'
        dropbox 'com.dropbox.core:dropbox-core-sdk:2.0.1'
        felix 'org.apache.felix:org.apache.felix.framework:5.4.0'
        fontawesomefx 'de.jensd:fontawesomefx-fontawesome:4.5.0'
        freemarker 'org.freemarker:freemarker:2.3.23'
        jsoup 'org.jsoup:jsoup:1.9.1'
        leapmotion fileTree(dir: allLibs, include: 'Leap/*')
        markdown 'com.github.rjeschke:txtmark:0.13'
        twitter4j 'org.twitter4j:twitter4j-core:4.0.4', 'org.twitter4j:twitter4j-stream:4.0.4'
        vertx 'io.vertx:vertx-core:3.2.1', 'io.vertx:vertx-web:3.2.1'
        wikitext fileTree(dir: new File(allLibs, 'WikiText'), include: '*.jar')
        zxing 'com.google.zxing:core:3.2.1', 'com.google.zxing:javase:3.2.1'

        junit 'junit:junit:4.12'
        mockito 'org.mockito:mockito-core:1.10.19'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'findbugs'
    apply plugin: 'jacoco'
    apply plugin: 'com.jfrog.bintray'

    group = "com.twasyl.slideshowfx"

    sourceSets {
        integrationTest {
            java {
                compileClasspath += main.output + test.output
                runtimeClasspath += main.output + test.output
                srcDir file('src/integration-test/java')
            }
            resources {
                srcDir 'src/integration-test/resources'
            }
        }
    }

    configurations {
        integrationTestCompile.extendsFrom testCompile
        integrationTestRuntime.extendsFrom testRuntime
    }

    task integrationTest(type: Test) {
        testClassesDir = sourceSets.integrationTest.output.classesDir
        classpath = sourceSets.integrationTest.runtimeClasspath
        outputs.upToDateWhen { false }
    }

    check.dependsOn integrationTest
    integrationTest.mustRunAfter test

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    test {
        useJUnit()
        reports.html.enabled = true
        reports.html.destination = file("${project.reporting.baseDir}/${name}")
    }

    findbugs {
        ignoreFailures = true
    }

    findbugsMain {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    jacocoTestReport {
        reports {
            xml.enabled = false
            csv.enabled = false
            html.enabled = true
        }
    }

    bintray {
        user =  project.findProperty('bintray_user_name') ?: System.getenv('BINTRAY_USER_NAME')
        key = project.findProperty('bintray_api_key') ?: System.getenv('BINTRAY_API_KEY')
        configurations = ['archives']

        pkg {
            repo = 'SlideshowFX'
            name = project.name
            websiteUrl = 'https://slideshowfx.github.io'
            issueTrackerUrl = 'https://github.com/twasyl/SlideshowFX/issues'
            vcsUrl = 'https://github.com/twasyl/SlideshowFX.git'

            desc = project.description

            licenses = ['Apache-2.0']

            version {
                name = project.version
                desc = project.description
            }
        }
    }
}

apply plugin: 'org.asciidoctor.convert'

asciidoctorj {
    version = '1.5.4'
}

asciidoctor {
    backends = ['html5','pdf']
    attributes = [
            'source-highlighter': 'coderay',
            toc: 'left',
            icons: 'font',
            setanchors: '',
            sectlinks: '',
            linkcss: false,
            'asciidoctor-source': new File(project(':SlideshowFX-app').projectDir, 'src/main/resources/com/twasyl/slideshowfx/documentation').absolutePath,
            'javafx-version': '8 update 77',
            'jdk-version': '8 update 77',
            'gradle-version': "${wrapper.gradleVersion}",
            'asciidoctorj-version': "${configurations.asciidoctorj.dependencies[0].version}",
            'apache-felix-version': "${configurations.felix.dependencies[0].version}",
            'freemarker-version': "${configurations.freemarker.dependencies[0].version}",
            'fontawesomefx-version' : "${configurations.fontawesomefx.dependencies[0].version}",
            'jsoup-version': "${configurations.jsoup.dependencies[0].version}",
            'leapmotion-sdk-version': '2.3.1.31549',
            'wikitext-core-version': '2.7.0.N20151015-1452',
            'wikitext-textile-core-version': '2.7.0.N20151009-1723',
            'twitter4j-version': "${configurations.twitter4j.dependencies[0].version}",
            'txtmark-version': "${configurations.markdown.dependencies[0].version}",
            'vertx-version': "${configurations.vertx.dependencies[0].version}",
            'zxing-core-version': "${configurations.zxing.dependencies[0].version}",
            'zxing-jse-version': "${configurations.zxing.dependencies[1].version}",
            'ace-version': '1.2.3'
    ]
}

task installMarkupPlugins(dependsOn: [':SlideshowFX-asciidoctor:jar',
                                      ':SlideshowFX-html:jar',
                                      ':SlideshowFX-markdown:jar',
                                      ':SlideshowFX-textile:jar']) << {

	copy {
		from project(':SlideshowFX-asciidoctor').jar.archivePath
        from project(':SlideshowFX-html').jar.archivePath
        from project(':SlideshowFX-markdown').jar.archivePath
        from project(':SlideshowFX-textile').jar.archivePath

		into pluginsFolder
	}
}

task installContentExtensionPlugins(dependsOn: [':SlideshowFX-alert-extension:jar',
                                               ':SlideshowFX-code-extension:jar',
                                               ':SlideshowFX-image-extension:jar',
                                               ':SlideshowFX-link-extension:jar',
                                               ':SlideshowFX-quiz-extension:jar',
                                               ':SlideshowFX-quote-extension:jar',
                                               ':SlideshowFX-sequence-diagram-extension:jar',
                                               ':SlideshowFX-snippet-extension:jar']) << {

    copy {
        from project(':SlideshowFX-alert-extension').jar.archivePath
        from project(':SlideshowFX-code-extension').jar.archivePath
        from project(':SlideshowFX-image-extension').jar.archivePath
        from project(':SlideshowFX-link-extension').jar.archivePath
        from project(':SlideshowFX-quiz-extension').jar.archivePath
        from project(':SlideshowFX-quote-extension').jar.archivePath
        from project(':SlideshowFX-sequence-diagram-extension').jar.archivePath
        from project(':SlideshowFX-snippet-extension').jar.archivePath

        into pluginsFolder
    }
}

task installHostingConnectorPlugins(dependsOn : [':SlideshowFX-dropbox-hosting-connector:jar',
                                         ':SlideshowFX-drive-hosting-connector:jar']) << {
    copy {
        from project(':SlideshowFX-dropbox-hosting-connector').jar.archivePath
        from project(':SlideshowFX-drive-hosting-connector').jar.archivePath

        into pluginsFolder
    }
}

task installSnippetExecutorPlugins(dependsOn : [':SlideshowFX-go-executor:jar',
                                                ':SlideshowFX-golo-executor:jar',
                                                ':SlideshowFX-groovy-executor:jar',
                                                ':SlideshowFX-java-executor:jar',
                                                ':SlideshowFX-javascript-executor:jar',
                                                ':SlideshowFX-kotlin-executor:jar',
                                                ':SlideshowFX-scala-executor:jar']) << {
    copy {
        from project(':SlideshowFX-go-executor').jar.archivePath
        from project(':SlideshowFX-golo-executor').jar.archivePath
        from project(':SlideshowFX-groovy-executor').jar.archivePath
        from project(':SlideshowFX-java-executor').jar.archivePath
        from project(':SlideshowFX-javascript-executor').jar.archivePath
        from project(':SlideshowFX-kotlin-executor').jar.archivePath
        from project(':SlideshowFX-scala-executor').jar.archivePath

        into pluginsFolder
    }
}

task installAllPlugins(dependsOn: [ ':installMarkupPlugins', ':installContentExtensionPlugins',
        ':installHostingConnectorPlugins', ':installSnippetExecutorPlugins']) << {
}

task packageSlideshowFX(dependsOn : [':SlideshowFX-asciidoctor:jar', ':SlideshowFX-html:jar', ':SlideshowFX-markdown:jar', ':SlideshowFX-textile:jar',
                                     ':SlideshowFX-code-extension:jar', ':SlideshowFX-image-extension:jar', ':SlideshowFX-link-extension:jar', ':SlideshowFX-quiz-extension:jar', ':SlideshowFX-quote-extension:jar', ':SlideshowFX-sequence-diagram-extension:jar', ':SlideshowFX-snippet-extension:jar',
                                     ':SlideshowFX-go-executor:jar', ':SlideshowFX-golo-executor:jar', ':SlideshowFX-groovy-executor:jar', ':SlideshowFX-java-executor:jar', ':SlideshowFX-javascript-executor:jar', ':SlideshowFX-kotlin-executor:jar', ':SlideshowFX-scala-executor:jar',
                                     ':SlideshowFX-app:jar',
                                     'asciidoctor']) {

    def destinationFolder = new File(buildDir, "/distributions/$applicationName-$applicationVersion")
    def pluginsFolder = new File(destinationFolder, "plugins")
    def docFolder = new File(destinationFolder, "docs")
    def tmpFolder = new File(destinationFolder, "tmp")

    // Create the JavaFX bundle
    ant.importBuild "${projectDir.absolutePath}/src/main/resources/javafx/javafx.xml"
    ant.classpath = "${jdk}/lib/ant-javafx.jar"

    ant.deployOutDir = "${buildDir.absolutePath}/distributions/packaging"
    ant.deployResourcesDir = tmpFolder.absolutePath

    doFirst {
        copy {
            from project(':SlideshowFX-asciidoctor').jar.archivePath
            from project(':SlideshowFX-html').jar.archivePath
            from project(':SlideshowFX-markdown').jar.archivePath
            from project(':SlideshowFX-textile').jar.archivePath

            into new File(pluginsFolder, "markups")
        }

        copy {
            from project(':SlideshowFX-alert-extension').jar.archivePath
            from project(':SlideshowFX-code-extension').jar.archivePath
            from project(':SlideshowFX-image-extension').jar.archivePath
            from project(':SlideshowFX-link-extension').jar.archivePath
            from project(':SlideshowFX-quiz-extension').jar.archivePath
            from project(':SlideshowFX-quote-extension').jar.archivePath
            from project(':SlideshowFX-sequence-diagram-extension').jar.archivePath
            from project(':SlideshowFX-snippet-extension').jar.archivePath

            into new File(pluginsFolder, "extensions")
        }

        copy {
            from project(':SlideshowFX-go-executor').jar.archivePath
            from project(':SlideshowFX-golo-executor').jar.archivePath
            from project(':SlideshowFX-groovy-executor').jar.archivePath
            from project(':SlideshowFX-java-executor').jar.archivePath
            from project(':SlideshowFX-javascript-executor').jar.archivePath
            from project(':SlideshowFX-kotlin-executor').jar.archivePath
            from project(':SlideshowFX-scala-executor').jar.archivePath

            into new File(pluginsFolder, "executors")
        }

        copy {
            from("${asciidoctor.outputDir}/html5") {
                include '*'
            }
            into("${docFolder.absolutePath}/html")
        }

        copy {
            from("${asciidoctor.outputDir}/pdf") {
                include '*.pdf'
            }
            into("${docFolder.absolutePath}/pdf")
        }

        // Copying libs
        copy {
            from project(':SlideshowFX-app').configurations.compile
            from(fileTree(dir: allLibs, include: "Leap/", exclude: "**/LeapJava.jar"))
            from project('SlideshowFX-app').jar.archivePath

            into(tmpFolder)
        }

        if(PlatformUtil.isMac()) {
            println "Create package for the OSX platform"
            deployOSX.execute()
        } else {
            println "Create package an unknown platform"
            deployUnknown.execute()
        }

        // Clean
        copy {
            from(fileTree(dir: new File(ant.deployOutDir, "bundles")))
            into(destinationFolder)
        }
    }

    doLast {
        delete { ant.deployOutDir }
        delete { tmpFolder }
        delete {asciidoctor.outputDir }
    }
}

task publishAllMarkup(dependsOn : [':SlideshowFX-asciidoctor:bintrayUpload',
                                   ':SlideshowFX-html:bintrayUpload',
                                   ':SlideshowFX-markdown:bintrayUpload',
                                   ':SlideshowFX-textile:bintrayUpload'] ) << {
}

task publishAllContentExtension(dependsOn: [':SlideshowFX-alert-extension:bintrayUpload',
                                            ':SlideshowFX-code-extension:bintrayUpload',
                                            ':SlideshowFX-image-extension:bintrayUpload',
                                            ':SlideshowFX-link-extension:bintrayUpload',
                                            ':SlideshowFX-quiz-extension:bintrayUpload',
                                            ':SlideshowFX-quote-extension:bintrayUpload',
                                            ':SlideshowFX-sequence-diagram-extension:bintrayUpload',
                                            ':SlideshowFX-snippet-extension:bintrayUpload']) << {

                                                }
task publishAllHostingConnector(dependsOn : [':SlideshowFX-dropbox-hosting-connector:bintrayUpload',
                                             ':SlideshowFX-drive-hosting-connector:bintrayUpload']) << {
}

task publishAllSnippetExecutor(dependsOn : [':SlideshowFX-go-executor:bintrayUpload',
                                            ':SlideshowFX-golo-executor:bintrayUpload',
                                            ':SlideshowFX-groovy-executor:bintrayUpload',
                                            ':SlideshowFX-java-executor:bintrayUpload',
                                            ':SlideshowFX-javascript-executor:bintrayUpload',
                                            ':SlideshowFX-kotlin-executor:bintrayUpload',
                                            ':SlideshowFX-scala-executor:bintrayUpload']) << {
}

task publishAllToBintray (dependsOn : ['publishAllMarkup',
                                       'publishAllContentExtension',
                                       'publishAllHostingConnector',
                                       'publishAllSnippetExecutor']) << {
}

wrapper {
    gradleVersion = '2.13'
}