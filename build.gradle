/*
 * Copyright 2015 Thierry Wasylczenko
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.2'
        classpath 'org.jruby:jruby-complete:1.7.18'
        classpath 'org.asciidoctor:asciidoctorj:1.5.2'
        classpath 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.6'
    }
}

subprojects {
    apply plugin: 'java'
    group = "com.twasyl.slideshowfx"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        jcenter()
        mavenCentral()
    }

    test {
        useTestNG()
        reports.html.enabled = true
    }
}

apply plugin: 'org.asciidoctor.convert'

ext {
    applicationName = 'SlideshowFX'
    applicationVersion = '1.0.0'

	allLibs = new File(rootDir, '/lib')
	jdk = System.env.'JAVA_HOME'

    pluginsFolder = new File(System.getProperty("user.home"), '.SlideshowFX/plugins')
    felixCacheFolder = new File(System.getProperty("user.home"), '.SlideshowFX/felix-cache')
}

ext.libraries = [
    asciidoctorj: ['org.jruby:jruby-complete:1.7.18', 'org.asciidoctor:asciidoctorj:1.5.2', 'org.slf4j:slf4j-simple:1.7.7'],
    drive: 'com.google.apis:google-api-services-drive:v2-rev155-1.19.1',
    dropbox:  ['com.dropbox.core:dropbox-core-sdk:1.7.7', 'com.fasterxml.jackson.core:jackson-core:2.2.2'],
    felix: 'org.apache.felix:org.apache.felix.framework:4.6.1@jar',
    freemarker: 'org.freemarker:freemarker:2.3.22@jar',
    jsoup: 'org.jsoup:jsoup:1.8.2@jar',
    leapmotion: fileTree(dir: allLibs, include: 'Leap/*'),
    markdown: 'com.github.rjeschke:txtmark:0.11',
    wikitext: fileTree(dir: new File(allLibs, 'WikiText'), include: '*.jar'),
    twitter4j: ['org.twitter4j:twitter4j-core:4.0.2@jar', 'org.twitter4j:twitter4j-stream:4.0.2@jar'],
    vertx: ['io.vertx:vertx-core:2.1.5@jar', 'io.vertx:vertx-platform:2.1.5@jar', 'io.netty:netty-all:4.0.21.Final', 'com.fasterxml.jackson.core:jackson-core:2.2.2', 'com.fasterxml.jackson.core:jackson-databind:2.2.2', 'com.fasterxml.jackson.core:jackson-annotations:2.2.2'],
    zxing: ['com.google.zxing:core:3.1.0@jar', 'com.google.zxing:javase:3.1.0@jar'],

    testng: ['org.testng:testng:6.8.13@jar', 'com.beust:jcommander:1.47'],
    cucumber: ['info.cukes:cucumber-core:1.2.2', 'info.cukes:cucumber-java:1.2.2', 'info.cukes:cucumber-testng:1.2.2'],
    automaton: 'com.athaydes.automaton:Automaton:1.1.0'

]

asciidoctorj {
    version = '1.5.2'
}

asciidoctor {
    backends = ['html5','pdf']
    attributes = [
            'source-highlighter': 'coderay',
            toc: 'left',
            icons: 'font',
            setanchors: '',
            sectlinks: '',
            linkcss: false,
            'javafx-version': '8 update 40',
            'jdk-version': '8 update 40',
            'gradle-version': '2.3',
            'asciidoctorj-version': '1.5.2',
            'apache-felix-version': '4.6.1',
            'commons-collections-version': '3.2.1',
            'commons-lang-version': '2.4',
            'freemarker-version': '2.3.21',
            'hamcrest-core-version': '1.3',
            'jackson-annotations-version': '2.2.2',
            'jackson-core-version': '2.2.2',
            'jackson-databind-version': '2.2.2',
            'jrubycomplete-version': '1.7.18',
            'jsoup-version': '1.8.2',
            'leapmotion-sdk-version': '2.2.5.26752',
            'netty-all-version': '4.0.21.Final',
            'wikitext-core-version': '2.2.0.N20140811-1615',
            'wikitext-textile-core-version': '2.2.0.N20140702-2138',
            'twitter4j-version': '4.0.2',
            'txtmark-version': '0.11',
            'vertx-version': '2.1.5',
            'zxing-core-version': '3.1.0',
            'zxing-jse-version': '3.1.0',
            'ace-version': '1.1.8'
    ]
}

task installMarkupPlugins(dependsOn: [':SlideshowFX-asciidoctor:jar',
                                      ':SlideshowFX-html:jar',
                                      ':SlideshowFX-markdown:jar',
                                      ':SlideshowFX-textile:jar']) << {

	copy {
		from project(':SlideshowFX-asciidoctor').jar.archivePath
        from project(':SlideshowFX-html').jar.archivePath
        from project(':SlideshowFX-markdown').jar.archivePath
        from project(':SlideshowFX-textile').jar.archivePath

		into pluginsFolder
	}
}

task installContentExtensionPlugins(dependsOn: [':SlideshowFX-alert-extension:jar',
                                               ':SlideshowFX-code-extension:jar',
                                               ':SlideshowFX-image-extension:jar',
                                               ':SlideshowFX-link-extension:jar',
                                               ':SlideshowFX-quote-extension:jar',
                                               ':SlideshowFX-sequence-diagram-extension:jar',
                                               ':SlideshowFX-snippet-extension:jar']) << {

    copy {
        from project(':SlideshowFX-alert-extension').jar.archivePath
        from project(':SlideshowFX-code-extension').jar.archivePath
        from project(':SlideshowFX-image-extension').jar.archivePath
        from project(':SlideshowFX-link-extension').jar.archivePath
        from project(':SlideshowFX-quote-extension').jar.archivePath
        from project(':SlideshowFX-sequence-diagram-extension').jar.archivePath
        from project(':SlideshowFX-snippet-extension').jar.archivePath

        into pluginsFolder
    }
}

task installHostingConnectorPlugins(dependsOn : [':SlideshowFX-dropbox-hosting-connector:jar',
                                         ':SlideshowFX-drive-hosting-connector:jar']) << {
    copy {
        from project(':SlideshowFX-dropbox-hosting-connector').jar.archivePath
        from project(':SlideshowFX-drive-hosting-connector').jar.archivePath

        into pluginsFolder
    }
}

task installSnippetExecutorPlugins(dependsOn : [':SlideshowFX-golo-executor:jar',
                                                ':SlideshowFX-java-executor:jar']) << {
    copy {
        from project(':SlideshowFX-golo-executor').jar.archivePath
        from project(':SlideshowFX-java-executor').jar.archivePath

        into pluginsFolder
    }
}

task packageSlideshowFX(dependsOn : [ ':SlideshowFX-asciidoctor:jar', ':SlideshowFX-html:jar', ':SlideshowFX-markdown:jar', ':SlideshowFX-textile:jar',
                                      ':SlideshowFX-code-extension:jar', ':SlideshowFX-image-extension:jar', ':SlideshowFX-link-extension:jar', ':SlideshowFX-quote-extension:jar', ':SlideshowFX-sequence-diagram-extension:jar', ':SlideshowFX-snippet-extension:jar',
                                      ':SlideshowFX-app:jar',
                                      'asciidoctor']) {

    def destinationFolder = new File(buildDir, "/distributions/$applicationName-$applicationVersion")
    def pluginsFolder = new File(destinationFolder, "plugins")
    def docFolder = new File(destinationFolder, "docs")
    def tmpFolder = new File(destinationFolder, "tmp")

    // Create the JavaFX bundle
    ant.importBuild "${projectDir.absolutePath}/src/main/resources/javafx/javafx.xml"
    ant.classpath = "${jdk}/lib/ant-javafx.jar"

    ant.deployOutDir = "${buildDir.absolutePath}/distributions/packaging"
    ant.deployResourcesDir = tmpFolder.absolutePath

    doFirst {
        copy {
            from project(':SlideshowFX-asciidoctor').jar.archivePath
            from project(':SlideshowFX-html').jar.archivePath
            from project(':SlideshowFX-markdown').jar.archivePath
            from project(':SlideshowFX-textile').jar.archivePath

            into new File(pluginsFolder, "markups")
        }

        copy {
            from project(':SlideshowFX-alert-extension').jar.archivePath
            from project(':SlideshowFX-code-extension').jar.archivePath
            from project(':SlideshowFX-image-extension').jar.archivePath
            from project(':SlideshowFX-link-extension').jar.archivePath
            from project(':SlideshowFX-quote-extension').jar.archivePath
            from project(':SlideshowFX-sequence-diagram-extension').jar.archivePath
            from project(':SlideshowFX-snippet-extension').jar.archivePath

            into new File(pluginsFolder, "extensions")
        }

        copy {
            from("${asciidoctor.outputDir}/html5") {
                include '*'
            }
            into("${docFolder.absolutePath}/html")
        }

        copy {
            from("${asciidoctor.outputDir}/pdf") {
                include '*.pdf'
            }
            into("${docFolder.absolutePath}/pdf")
        }

        // Copying libs
        copy {
            from project(':SlideshowFX-app').configurations.compile
            from(fileTree(dir: allLibs, include: "Leap/", exclude: "**/LeapJava.jar"))
            from project('SlideshowFX-app').jar.archivePath

            into(tmpFolder)
        }

        javafxdeploy.execute()

        // Clean
        copy {
            from(fileTree(dir: new File(ant.deployOutDir, "bundles")))
            into(destinationFolder)
        }
    }

    doLast {
        delete { ant.deployOutDir }
        delete { tmpFolder }
        delete {asciidoctor.outputDir }
    }
}

wrapper {
    gradleVersion = '2.3'
}