description = 'Extension allowing to insert alerts inside a SlideshowFX presentation'
version = '1.3-SNAPSHOT'

apply plugin: 'java-library'

dependencies {
    api project(':slideshowfx-content-extension')
    implementation project(':slideshowfx-icons')
    api project(':slideshowfx-markup')
    api project(':slideshowfx-plugin')
    api project(':slideshowfx-ui-controls')

    testCompile "org.mockito:mockito-core:${rootProject.ext.dependencies.mockito.version}"
}

ext.isPlugin = true
ext.isContentExtension = true
ext.bundle = [
        name               : 'SlideshowFX alert extension',
        symbolicName       : 'com.twasyl.slideshowfx.content.extension.alert',
        description        : 'Support for inserting alert in slides',
        activator          : 'com.twasyl.slideshowfx.content.extension.alert.activator.AlertContentExtensionActivator',
        exportPackage      : 'com.twasyl.slideshowfx.content.extension.alert.controllers,com.twasyl.slideshowfx.content.extension.alert,com.twasyl.slideshowfx.content.extension.alert.activator',
        setupWizardLabel   : 'Alert',
        setupWizardIconName: 'EXCLAMATION_TRIANGLE'
]

task createSweetAlertPackage() {
    doLast {
        def sweetAlertVersion = project.findProperty('sweetAlertVersion') ?: ''
        def binaryDir = new File("${buildDir}/tmp/sweetalert-update")
        def binary = new File(binaryDir, "binary.zip")
        def newPackageDir = new File("${binaryDir}/sweetalert2", sweetAlertVersion)
        def unpackedBinary = new File("${binaryDir}/sweetalert2-${sweetAlertVersion}")
        if (!binaryDir.exists()) {
            binaryDir.mkdirs()
        }

        new URL("https://github.com/sweetalert2/sweetalert2/archive/v${sweetAlertVersion}.zip").withInputStream { i ->
            binary.withOutputStream {
                it << i
            }
        }

        copy {
            from zipTree(binary)
            into binaryDir
        }

        binary.delete()

        if (newPackageDir.exists()) {
            newPackageDir.deleteDir()
        }

        copy {
            from fileTree("${unpackedBinary}/dist")
            include "sweetalert2.all.min.js"
            into newPackageDir
        }

        unpackedBinary.deleteDir()
    }
}

task zipSweetAlertPackage(type: Zip) {
    archiveName = "sweetalert2.zip"
    destinationDir = file("${buildDir}/tmp")
    from("${buildDir}/tmp/sweetalert-update/sweetalert2") {
        into "sweetalert2"
    }
}

createSweetAlertPackage.finalizedBy zipSweetAlertPackage